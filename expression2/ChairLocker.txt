#
# DO NOT MODIFY OR REMOVE THIS HEADER!
#
# CREDITS/CONTRIBUTORS:
#   Originally developed and maintained by STEAM_0:0:65979910 (http://steamcommunity.com/profiles/76561198092225548/ - STEAM_0:0:65979910; U:1:131959820); TheCaptainPrice a.k.a. CaptainPRICE.
#
# LICENSE:
#   GNU General Public License version 3.
#
# DESCRIPTION:
#   Simple and effective Expression 2 script which allows you (and players you trust) to easily lock any chair/seat owned by you (or player you trust).
#
# SETUP / INSTALLATION INSTRUCTIONS:
#   Spawn this E2 on chair/seat to lock it (or use chat command (case-insensitive): lockchair <entity ID>).
#   To unlock chair/seat, remove this E2 from it (or use chat command (case-insensitive): unlockchair <entity ID>).
#   It is also possible to toggle current state using "togglechair" (case-insensitive) chat-command (but first aim on E2/chair or enter a chair/seat to toggle, notice this chat-command does not take any arguments).
#   Good feature about this is, it works independently and allows multiple chairs to be controlled (just need to spawn a separate ChairLocker E2 on each).
#   If you want chair/seat to remove when you remove E2, wire/set "ChairRemovalOnE2Remove" input to 1 (note: requires "propcore" extension enabled).
#   If you want to automatically eject the driver (if any) when the vehicle gets locked, wire/set "EjectDriverOnLock" input to 1.
#
# Repository URL: https://github.com/CaptainPRICE/Wire-Data
# For more details, please visit a thread on the forums: http://www.wiremod.com/forum/finished-contraptions/35272-easy-vehicle-locker-unlocker-e2.html
# In this E2, I have implemented a bit more advanced chat-command parsing.
# As always, have fun!
#

@name ChairLocker
@inputs [ChairRemovalOnE2Remove EjectDriverOnLock]
@outputs Chair:entity IsLocked
@persist [STATE_LOCK STATE_UNLOCK]
@model models/bull/gates/processor.mdl
@trigger none

if ( first() )
{
    Chair = entity():isWeldedTo()
    if ( !Chair:isVehicle() )
    {
        error( "Not spawned on the chair/seat (vehicle)." )
    }

    STATE_LOCK = 1

    #
    # A function that is called after IsLocked is changed (used to lock or unlock the chair depending on the value of IsLocked).
    #
    function handleLock( Player:entity )
    {
        if ( !Player:isPlayer() )
        {
            return
        }
#ifdef entity:propNotSolid( number )
        Chair:propNotSolid( IsLocked )
#endif
        Chair:lockPod( IsLocked )
        local Message = ""
        if ( IsLocked )
        {
            if ( ->EjectDriverOnLock & EjectDriverOnLock )
            {
                Chair:ejectPod()
            }
            Message = "ChairLocker: " + Player:name() + " locked " + toString( Chair ) + "."
            setName( "Chair Locker\nState: Locked" )
        }
        else
        {
            Message = "ChairLocker: " + Player:name() + " unlocked " + toString( Chair ) + "."
            setName( "Chair Locker\nState: Unlocked" )
        }
        print( Message )
        hint( Message, 7 )
    }

    #
    # A function that occurs whenever the valid player (either you or player you trust) said "lockchair" or "unlockchair" in chat (case-insensitive).
    #
    function onChairChatCommandHandler( LastSpoke:entity, LastSaidTrim:string, SplitLastSaid:array, State:number )
    {
        if ( LastSpoke == owner() )
        {
            hideChat( 1 )
        }
        if ( IsLocked & State == STATE_LOCK )
        {
            print( "ChairLocker: Attempt to lock already locked vehicle." )
            return
        }
        if ( !IsLocked & State == STATE_UNLOCK )
        {
            print( "ChairLocker: Attempt to unlock already unlocked vehicle." )
            return
        }
        local Command = SplitLastSaid:removeString( 1 )
        LastSaidTrim = SplitLastSaid:concat( " " ):trim()
        local String = ""
        for ( I = 1, LastSaidTrim:length(), 1 )
        {
            local Char = LastSaidTrim:index( I )
            local Byte = toByte( Char )
            if ( Byte < 48 | Byte > 57 ) # Accept digits only.
            {
                print( "ChairLocker: Found unexpected character '" + Char + "' (index=" + I + ")." )
                return
            }
            String += Char
        }
        local NumberExpression = String:match( "^(%d+)$" ) # TODO: Capture multiple IDs (separated by comma), to allow locking/unlocking multiple vehicles using a single command.
        if ( NumberExpression:count() != 1 )
        {
            print( "ChairLocker: Invalid command syntax (use " + Command + " <entity ID>, e.g. " + Command + " " + Chair:id() + ")." )
            return
        }
        if ( NumberExpression[ 1, string ]:toNumber() != Chair:id() )
        {
            return
        }
        IsLocked = State == STATE_LOCK
        handleLock( LastSpoke )
    }

#ifdef entity:propFreeze( number )
    Chair:propFreeze( 1 )
#endif
    entity():setSkin( 1 )
    IsLocked = STATE_LOCK # Default state when E2 is spawned; STATE_LOCK = lock chair/seat.
    handleLock( owner() )
    runOnChat( 1 )
    runOnLast( 1 )
}
elseif ( chatClk() )
{
    local LastSpoke = lastSpoke()
    if ( !LastSpoke:isPlayer() | ( LastSpoke != owner() & !owner():trusts( LastSpoke ) ) )
    {
        exit()
    }
    local Trim = LastSpoke:lastSaid():trim()
    local Exp = Trim:explode( " " )
    switch ( Exp[ 1, string ]:lower() )
    {
        case "lockchair",
        case "lockseat",
        case "lockvehicle",
            onChairChatCommandHandler( LastSpoke, Trim, Exp, STATE_LOCK )
            break
        case "unlockchair",
        case "unlockseat",
        case "unlockvehicle",
            onChairChatCommandHandler( LastSpoke, Trim, Exp, STATE_UNLOCK )
            break
        case "togglechair",
        case "toggleseat",
        case "togglestate",
        case "togglevehicle",
            hideChat( 1 )
            if ( ( !LastSpoke:inVehicle() | LastSpoke:vehicle() != Chair ) & LastSpoke:aimEntity() != entity() & LastSpoke:aimEntity() != Chair )
            {
                break
            }
            IsLocked = !IsLocked
            handleLock( LastSpoke )
            break
    }
}
elseif ( last() )
{
    if ( ->ChairRemovalOnE2Remove & ChairRemovalOnE2Remove )
    {
#ifdef entity:propDelete()
        # Remove/delete chair/seat when E2 gets removed.
        if ( Chair:isVehicle() )
        {
            Chair:propDelete()
        }
#endif
    }
    IsLocked = STATE_UNLOCK
    Chair:lockPod( IsLocked ) # Unlock a chair.
}
if ( !Chair:isVehicle() )
{
    # Remove E2 when chair/seat gets removed.
    selfDestruct()
}
